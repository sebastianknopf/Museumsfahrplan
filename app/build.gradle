apply plugin: 'com.android.application'

android {
    compileSdkVersion 27

    def versionPropsFile = file('version.build')

    if (versionPropsFile.canRead()) {
        def Properties versionProps = new Properties()
        versionProps.load(new FileInputStream(versionPropsFile))

        def buildNumber = versionProps['BUILD_NUMBER'].toInteger() + 1

        versionProps['BUILD_NUMBER'] = buildNumber.toString()
        versionProps.store(versionPropsFile.newWriter(), null)

        defaultConfig {
            applicationId "de.mfpl.app"
            minSdkVersion 21
            targetSdkVersion 27
            versionCode buildNumber
            versionName "1.0 " + buildNumber
            testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
            multiDexEnabled true

            vectorDrawables.useSupportLibrary = true
        }
    } else {
        throw new GradleException("Could not read version.build!")
    }

    // needed only for MapBox!
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    dataBinding {
        enabled true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.android.support:recyclerview-v7:27.0.0'
    implementation 'com.android.support:support-annotations:28.0.0'
    implementation 'com.android.support:design:27.1.1'

    // external libraries for mfpl
    implementation 'com.mapbox.mapboxsdk:mapbox-android-sdk:6.7.1'
    implementation 'com.google.android.gms:play-services-location:15.0.1'

    implementation project(':staticnet')

    // required for test implementation
    testImplementation 'junit:junit:4.12'

    // required for android test implementation
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
